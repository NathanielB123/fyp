{-# OPTIONS --prop --rewriting --show-irrelevant #-}

open import Utils
-- open import STLC.BoolRw.Syntax
-- open import STLC.SubstEq
open import STLC.Syntax2
open import STLC.SubstEq2
open import STLC.BoolRw.Views

module STLC.BoolRw.SpontRed where

data Sortâ†’ : Set where
  Î² rw : Sortâ†’ 

variable
  qâ†’ râ†’ sâ†’ : Sortâ†’

-- This relation is an over-approximation of the reduction rules we actually
-- plan on using. E.g. it allows rewriting arbitrary boolean terms 
-- "spontaneously" to 'true' or 'false' with no restrictions.
--
-- The idea is that accessibility w.r.t. this over-approximation implies
-- accessibility w.r.t. the "true" reduction relation.
data _[_]â†’_ : Tm Î“ A â†’ Sortâ†’ â†’ Tm Î“ A â†’ Set where
  -- We do a little Fording
  Î²         : âˆ€ {Æ›t t[u]} â†’ Æ›t â‰¡ Æ› t â†’ t[u] â‰¡ t [ < u > ] â†’ (Æ›t Â· u) [ Î² ]â†’ t[u]
  rec-true  : ğ”¹-rec true u v [ Î² ]â†’ u
  rec-false : ğ”¹-rec false u v [ Î² ]â†’ v
  rw        : Â¬ t/f {A = ğ”¹'} t â†’ t/f {A = ğ”¹'} u â†’ t [ rw ]â†’ u

  lÂ·     : tâ‚ [ qâ†’ ]â†’ tâ‚‚ â†’ (tâ‚ Â· u) [ qâ†’ ]â†’ (tâ‚‚ Â· u) 
  Â·r     : uâ‚ [ qâ†’ ]â†’ uâ‚‚ â†’ (t Â· uâ‚) [ qâ†’ ]â†’ (t Â· uâ‚‚)
  Æ›_     : tâ‚ [ qâ†’ ]â†’ tâ‚‚ â†’ (Æ› tâ‚)   [ qâ†’ ]â†’ (Æ› tâ‚‚)
  ğ”¹-recâ‚ : tâ‚ [ qâ†’ ]â†’ tâ‚‚ â†’ ğ”¹-rec tâ‚ u v [ qâ†’ ]â†’ ğ”¹-rec tâ‚‚ u v
  ğ”¹-recâ‚‚ : uâ‚ [ qâ†’ ]â†’ uâ‚‚ â†’ ğ”¹-rec t uâ‚ v [ qâ†’ ]â†’ ğ”¹-rec t uâ‚‚ v
  ğ”¹-recâ‚ƒ : vâ‚ [ qâ†’ ]â†’ vâ‚‚ â†’ ğ”¹-rec t u vâ‚ [ qâ†’ ]â†’ ğ”¹-rec t u vâ‚‚
  +-recâ‚ : tâ‚ [ qâ†’ ]â†’ tâ‚‚ â†’ +-rec tâ‚ u v [ qâ†’ ]â†’ +-rec tâ‚‚ u v
  +-recâ‚‚ : uâ‚ [ qâ†’ ]â†’ uâ‚‚ â†’ +-rec t uâ‚ v [ qâ†’ ]â†’ +-rec t uâ‚‚ v
  +-recâ‚ƒ : vâ‚ [ qâ†’ ]â†’ vâ‚‚ â†’ +-rec t u vâ‚ [ qâ†’ ]â†’ +-rec t u vâ‚‚

  -- TODO: Add beta laws for coproducts
  inl    : tâ‚ [ qâ†’ ]â†’ tâ‚‚ â†’ inl {B = B} tâ‚ [ qâ†’ ]â†’ inl tâ‚‚
  inr    : tâ‚ [ qâ†’ ]â†’ tâ‚‚ â†’ inr {A = A} tâ‚ [ qâ†’ ]â†’ inr tâ‚‚

_â†’Î²_ : Tm Î“ A â†’ Tm Î“ A â†’ Set
_â†’Î²_ = _[ Î² ]â†’_

_â†’rw_ : Tm Î“ A â†’ Tm Î“ A â†’ Set
_â†’rw_ = _[ rw ]â†’_

_[_]â†’ : t [ qâ†’ ]â†’ u â†’ (Î´ : Vars Î” Î“) 
      â†’ (t [ Î´ ]) [ qâ†’ ]â†’ (u [ Î´ ])
Î² refl refl [ Î´ ]â†’ = Î² refl refl
rec-true    [ Î´ ]â†’ = rec-true
rec-false   [ Î´ ]â†’ = rec-false
rw Â¬b b     [ Î´ ]â†’ = rw (Â¬b [ Î´ ]Â¬b) (b [ Î´ ]b) 
lÂ· p        [ Î´ ]â†’ = lÂ· (p [ Î´ ]â†’)
Â·r p        [ Î´ ]â†’ = Â·r (p [ Î´ ]â†’)
ğ”¹-recâ‚ p    [ Î´ ]â†’ = ğ”¹-recâ‚ (p [ Î´ ]â†’)
ğ”¹-recâ‚‚ p    [ Î´ ]â†’ = ğ”¹-recâ‚‚ (p [ Î´ ]â†’)
ğ”¹-recâ‚ƒ p    [ Î´ ]â†’ = ğ”¹-recâ‚ƒ (p [ Î´ ]â†’)
+-recâ‚ p    [ Î´ ]â†’ = +-recâ‚ (p [ Î´ ]â†’)
+-recâ‚‚ p    [ Î´ ]â†’ = +-recâ‚‚ (p [ Î´ ^ _ ]â†’)
+-recâ‚ƒ p    [ Î´ ]â†’ = +-recâ‚ƒ (p [ Î´ ^ _ ]â†’)
inl p       [ Î´ ]â†’ = inl (p [ Î´ ]â†’)
inr p       [ Î´ ]â†’ = inr (p [ Î´ ]â†’)
(Æ› p)       [ Î´ ]â†’ = Æ› (p [ Î´ ^ _ ]â†’)

Æ›[_]â»Â¹_ : (Î´ : Vars Î” Î“) â†’ t [ Î´ ] â‰¡ Æ› u â†’ âˆƒ Î» uâ»Â¹ â†’ t â‰¡ Æ› uâ»Â¹
Æ›[_]â»Â¹_ {t = Æ› t} Î´ refl = _ Î£, refl 

[_]â†’â»Â¹_ : âˆ€ (Î´ : Vars Î” Î“) â†’ (t [ Î´ ]) [ qâ†’ ]â†’ u
        â†’ âˆƒ Î» uâ»Â¹ â†’ (t [ qâ†’ ]â†’ uâ»Â¹) Ã— (uâ»Â¹ [ Î´ ] â‰¡ u)
[_]â†’â»Â¹_ {t = t Â· u} Î´ (Î² p q) with tâ€² Î£, refl â† Æ›[_]â»Â¹_ {t = t} Î´ p
                               with refl       â† p
                               with refl       â† q
  = _ Î£, Î² refl refl Î£, refl
[_]â†’â»Â¹_ {t = ğ”¹-rec true u v}  Î´ rec-true  = u Î£, rec-true  Î£, refl
[_]â†’â»Â¹_ {t = ğ”¹-rec false u v} Î´ rec-false = v Î£, rec-false Î£, refl

[_]â†’â»Â¹_ {t = Æ› t} Î´ (Æ› p) 
  = let uâ»Â¹   Î£, pâ»Â¹   Î£, q = [_]â†’â»Â¹_ {t = t} (Î´ ^ _) p 
     in Æ› uâ»Â¹ Î£, Æ› pâ»Â¹ Î£, cong Æ›_ q

[_]â†’â»Â¹_ {t = t Â· u} Î´ (lÂ· p)
  = let tâ»Â¹     Î£, pâ€²    Î£, q = [_]â†’â»Â¹_ {t = t} Î´ p
     in tâ»Â¹ Â· u Î£, lÂ· pâ€² Î£, cong (_Â· (u [ Î´ ])) q
[_]â†’â»Â¹_ {t = t Â· u} Î´ (Â·r p)
  = let uâ»Â¹     Î£, pâ€²    Î£, q = [_]â†’â»Â¹_ {t = u} Î´ p
     in t Â· uâ»Â¹ Î£, Â·r pâ€² Î£, cong (t [ Î´ ] Â·_) q

[_]â†’â»Â¹_ {t = ğ”¹-rec t u v} Î´ (ğ”¹-recâ‚ p) 
  = let tâ»Â¹           Î£, pâ€²        Î£, q = [_]â†’â»Â¹_ {t = t} Î´ p
     in ğ”¹-rec tâ»Â¹ u v Î£, ğ”¹-recâ‚ pâ€² Î£, cong (Î» â–¡ â†’ ğ”¹-rec â–¡ (u [ Î´ ]) (v [ Î´ ])) q
[_]â†’â»Â¹_ {t = ğ”¹-rec t u v} Î´ (ğ”¹-recâ‚‚ p) 
  = let uâ»Â¹           Î£, pâ€²        Î£, q = [_]â†’â»Â¹_ {t = u} Î´ p
     in ğ”¹-rec t uâ»Â¹ v Î£, ğ”¹-recâ‚‚ pâ€² Î£, cong (Î» â–¡ â†’ ğ”¹-rec (t [ Î´ ]) â–¡ (v [ Î´ ])) q
[_]â†’â»Â¹_ {t = ğ”¹-rec t u v} Î´ (ğ”¹-recâ‚ƒ p) 
  = let vâ»Â¹           Î£, pâ€²        Î£, q = [_]â†’â»Â¹_ {t = v} Î´ p
     in ğ”¹-rec t u vâ»Â¹ Î£, ğ”¹-recâ‚ƒ pâ€² Î£, cong (Î» â–¡ â†’ ğ”¹-rec (t [ Î´ ]) (u [ Î´ ]) â–¡) q

[_]â†’â»Â¹_ {t = +-rec t u v} Î´ (+-recâ‚ p) 
  = let tâ»Â¹           Î£, pâ€²        Î£, q = [_]â†’â»Â¹_ {t = t} Î´ p
     in +-rec tâ»Â¹ u v Î£, +-recâ‚ pâ€² 
     Î£, cong (Î» â–¡ â†’ +-rec â–¡ (u [ Î´ ^ _ ]) (v [ Î´ ^ _ ])) q
[_]â†’â»Â¹_ {t = +-rec t u v} Î´ (+-recâ‚‚ p) 
  = let uâ»Â¹           Î£, pâ€²        Î£, q = [_]â†’â»Â¹_ {t = u} (Î´ ^ _) p
     in +-rec t uâ»Â¹ v Î£, +-recâ‚‚ pâ€² 
     Î£, cong (Î» â–¡ â†’ +-rec (t [ Î´ ]) â–¡ (v [ Î´ ^ _ ])) q
[_]â†’â»Â¹_ {t = +-rec t u v} Î´ (+-recâ‚ƒ p) 
  = let vâ»Â¹           Î£, pâ€²        Î£, q = [_]â†’â»Â¹_ {t = v} (Î´ ^ _) p
     in +-rec t u vâ»Â¹ Î£, +-recâ‚ƒ pâ€² 
     Î£, cong (Î» â–¡ â†’ +-rec (t [ Î´ ]) (u [ Î´ ^ _ ]) â–¡) q


[_]â†’â»Â¹_ {t = inl t} Î´ (inl p)
  = let tâ»Â¹     Î£, pâ€²     Î£, q = [_]â†’â»Â¹_ {t = t} Î´ p
     in inl tâ»Â¹ Î£, inl pâ€² Î£, cong inl q
[_]â†’â»Â¹_ {t = inr t} Î´ (inr p)
  = let tâ»Â¹     Î£, pâ€²     Î£, q = [_]â†’â»Â¹_ {t = t} Î´ p
     in inr tâ»Â¹ Î£, inr pâ€² Î£, cong inr q

[_]â†’â»Â¹_ {u = true}  Î´ (rw Â¬b _) = true  Î£, rw ([ Î´ ]Â¬bâ»Â¹ Â¬b) true  Î£, refl
[_]â†’â»Â¹_ {u = false} Î´ (rw Â¬b _) = false Î£, rw ([ Î´ ]Â¬bâ»Â¹ Â¬b) false Î£, refl

record SNâ†’ Î“ A (t : Tm Î“ A) : Set where
  constructor acc
  inductive
  pattern
  field
    step : t [ qâ†’ ]â†’ u â†’ SNâ†’ Î“ A u 
open SNâ†’ public

SNâ†’lÂ· : SNâ†’ Î“ B (t Â· u) â†’ SNâ†’ Î“ (A â‡’ B) t
SNâ†’lÂ· (acc f) = acc (Î» p â†’ SNâ†’lÂ· (f (lÂ· p)))

SNâ†’Â·r : SNâ†’ Î“ B (t Â· u) â†’ SNâ†’ Î“ A u
SNâ†’Â·r (acc f) = acc (Î» p â†’ SNâ†’Â·r (f (Â·r p)))

SNâ†’ğ”¹-recâ‚ : SNâ†’ Î“ A (ğ”¹-rec t u v) â†’ SNâ†’ Î“ ğ”¹' t
SNâ†’ğ”¹-recâ‚ (acc f) = acc (Î» p â†’ SNâ†’ğ”¹-recâ‚ (f (ğ”¹-recâ‚ p)))

SNâ†’ğ”¹-recâ‚‚ : SNâ†’ Î“ A (ğ”¹-rec t u v) â†’ SNâ†’ Î“ A u
SNâ†’ğ”¹-recâ‚‚ (acc f) = acc (Î» p â†’ SNâ†’ğ”¹-recâ‚‚ (f (ğ”¹-recâ‚‚ p)))

SNâ†’ğ”¹-recâ‚ƒ : SNâ†’ Î“ A (ğ”¹-rec t u v) â†’ SNâ†’ Î“ A v
SNâ†’ğ”¹-recâ‚ƒ (acc f) = acc (Î» p â†’ SNâ†’ğ”¹-recâ‚ƒ (f (ğ”¹-recâ‚ƒ p)))

true-sn : SNâ†’ Î“ ğ”¹' true
true-sn = acc (Î» where (rw Â¬b _) â†’ âŠ¥-elim (Â¬b true))

false-sn : SNâ†’ Î“ ğ”¹' false
false-sn = acc (Î» where (rw Â¬b _) â†’ âŠ¥-elim (Â¬b false))

_[_]sn : SNâ†’ Î“ A t â†’ âˆ€ (Î´ : Vars Î” Î“) â†’ SNâ†’ Î” A (t [ Î´ ])
acc a [ Î´ ]sn = acc (Î» p â†’ let uâ»Â¹ Î£, q Î£, r = [ Î´ ]â†’â»Â¹ p 
                            in subst (SNâ†’ _ _) r (a q [ Î´ ]sn))

[_]snâ»Â¹_ : (Î´ : Vars Î” Î“) â†’ SNâ†’ Î” A (t [ Î´ ]) â†’ SNâ†’ Î“ A t
[ Î´ ]snâ»Â¹ acc a = acc (Î» p â†’ [ Î´ ]snâ»Â¹ a (p [ Î´ ]â†’))
  